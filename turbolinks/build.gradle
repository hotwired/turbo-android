apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.ben-manes.versions'

// Update when releasing a new library version
ext.libVersionCode = 82
ext.libVersionName = '1.0.82'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    //  classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.29.0'
    }
}

repositories {
    google()
    mavenCentral()
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29
        versionCode libVersionCode
        versionName libVersionName

        // Define ProGuard rules for this android library project. These rules will be applied when
        // a consumer of this library sets 'minifyEnabled true'.
        consumerProguardFiles 'proguard-consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.4.0'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.0"
    implementation 'com.google.android.material:material:1.2.0'

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.webkit:webkit:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // JSON
    implementation 'com.google.code.gson:gson:2.8.6'

    // Networking/API
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.1'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'

    // Tests
    testImplementation 'androidx.test:core:1.3.0' // Robolectric
    testImplementation 'androidx.navigation:navigation-testing:2.3.0'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9'
    testImplementation 'org.assertj:assertj-core:3.17.0'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'org.mockito:mockito-core:3.5.7'
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.8.1'
    testImplementation 'junit:junit:4.13'
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'turbolinks-android'

    publishedGroupId = 'com.basecamp'
    artifact = 'turbolinks'

    libraryName = 'Turbolinks Android'
    libraryDescription = 'Turbolinks for Android'
    libraryVersion = "$libVersionName"

    siteUrl = 'https://github.com/basecamp/turbolinks-android'
    gitUrl = 'https://github.com/basecamp/turbolinks-android.git'

    developerId = 'basecamp'
    developerName = 'Basecamp'
    developerEmail = 'support@basecamp.com'
}

// Maven
group = publishedGroupId
version = libraryVersion

//install {
//    repositories.mavenInstaller {
//        // This generates POM.xml with proper parameters
//        pom {
//            project {
//                packaging 'aar'
//                groupId publishedGroupId
//                artifactId artifact
//
//                // Add your description here
//                name libraryName
//                description libraryDescription
//
//                developers {
//                    developer {
//                        id developerId
//                        name developerName
//                        email developerEmail
//                    }
//                }
//            }
//        }
//    }
//}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//artifacts {
//    archives javadocJar
//    archives sourcesJar
//}

// Bintray
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Rejected')
            }
        }
    }
}

apply plugin: 'maven'

uploadArchives {
    repositories.mavenDeployer {
        def deployPath = file("artifacts")
        repository(url: "file://${deployPath.absolutePath}")
        pom.project {
            groupId 'com.basecamp'
            artifactId 'turbolinks-android'
            version libVersionName
        }
    }
}
